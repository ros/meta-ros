# We have a conf and classes directory, append to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a recipes directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "meta-webos-ros"
BBFILE_PATTERN_meta-webos-ros := "^${LAYERDIR}/"
BBFILE_PRIORITY_meta-webos-ros = "52"

# Version 1 was the manually created meta-ros, prior to the introduction of superflore.
# Version 2 was all ROS distributions in the same meta-ros layer with right subdirectories selected by ROS_DISTRO variable
# Version 3 is with each ROS distribution in separate sublayer of meta-ros git repository (with modifications to generated recipes applied in .bbappends instead of .inc files)
LAYERVERSION_meta-webos-ros = "3"

LAYERDEPENDS_meta-webos-ros = " \
    core \
    meta-python \
    openembedded-layer \
    ros-common-layer \
"

LAYERSERIES_COMPAT_meta-webos-ros = "thud"

# Ignore metadata that requires meta-webos-raspberrypi if it's not present.
BBMASK +=  "${@bb.utils.contains('BBFILE_COLLECTIONS', 'meta-webos-raspberrypi', '', 'recipes-kernel/linux/linux-raspberrypi_4.4.bbappend', d)}"
BBMASK +=  "${@bb.utils.contains('BBFILE_COLLECTIONS', 'meta-webos-raspberrypi', '', 'recipes-graphics/userland/userland_git.bbappend', d)}"

# Don't allow ROS-only images to be built with DISTRO set to "webos" as they will contain different platform packages, eg, the
# kernel, from images with the same names built with DISTRO set to "ros1" or "ros2". Using BBMASK instead of PNBLACKLIST because
# a RE name match is wanted.
BBMASK += "recipes-core/images/core-image-ros-.*"
