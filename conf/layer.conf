# We have a conf and classes directory, append to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a recipes directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "ros-layer"
BBFILE_PATTERN_ros-layer := "^${LAYERDIR}/"
BBFILE_PRIORITY_ros-layer = "7"

LICENSE_PATH += "${LAYERDIR}/licenses"

# Version 1 was the manually created meta-ros, prior to the introduction of superflore.
LAYERVERSION_ros-layer = "2"

LAYERDEPENDS_ros-layer = " \
    core \
    meta-python \
    openembedded-layer \
"

# We only need meta-qt5 if Qt5 has been enabled.
LAYERDEPENDS_ros-layer += "${@bb.utils.contains('DISTRO_FEATURES', 'ros-qt5', 'qt5-layer', '', d)}"

# We only need meta-intel-realsense if RealSense has been enabled.
LAYERDEPENDS_ros-layer += "${@bb.utils.contains('DISTRO_FEATURES', 'ros-realsense', 'intel-realsense', '', d)}"

require conf/ros-distro/ros-distro.conf

# Only .bb files are generated
BBFILES += "${LAYERDIR}/generated-recipes-${ROS_DISTRO}/*/*.bb"

# This is only temporary: in layer version 3, this will be resolved in .bbappend-s
# and reported upstream like:
# https://github.com/ANYbotics/rqt_multiplot_plugin/issues/35
# https://github.com/ANYbotics/variant-release/issues/1
# https://github.com/orocos/orocos-kdl-release/issues/4
# https://github.com/ros2-gbp/orocos_kinematics_dynamics-release/issues/2
# https://github.com/ros-perception/slam_karto/issues/28
# https://github.com/SteveMacenski/navigation2-release/issues/1
# Be aware that this isn't accurate or even correct, some of these actually have
# e.g. LGPL-3.0, Apache-2, BSD, CC-BY-* licenses instead
SPDXLICENSEMAP[LGPL-2] = "LGPL-2.0"
