# Copyright (c) 2019-2020 LG Electronics, Inc.
#
# Configuration settings common to all ROS_DISTRO-s.

# All generated recipes have this set to "1" because they inherit ros_superflore_generated.
ROS_SUPERFLORE_GENERATED ??= "0"

# Ensure that this always has a value -- it's only explicitly set in generated recipes.
ROS_BUILD_TYPE ??= "unset"

# ROS_DISTRO_COMPAT contains the list of ROS_DISTRO-s with which this branch of meta-ros is compatible (based on the versions of
# the platform packages provided by it and the branches of the upstream OE layers used).
ROS_DISTRO_COMPAT = "melodic dashing eloquent"

# This variable is updated with the new release series as the first commit to [master] after the branch for the current release
# series is created.
ROS_OE_RELEASE_SERIES = "warrior"

# "1" or "2"
ROS_DISTRO_METADATA_VERSION_MAJOR = "${ROS_VERSION}"

# Could covert the first letter to an integer, but everyone knows the ROS distro-s by their names, so just use it.
ROS_DISTRO_METADATA_VERSION_MINOR = "${ROS_DISTRO}"

# Crystal releases have been given patch numbers, but none of the others have them => use the release date (YYYYMMDD) for the
# micro version, unless this is prior to the first release of a ROS_DISTRO, when the date portion of
# ROS_SUPERFLORE_GENERATION_DATETIME is used.
ROS_DISTRO_METADATA_VERSION_MICRO = "${@d.getVar('ROS_SUPERFLORE_GENERATION_DATETIME', True)[0:8] \
                                        if d.getVar('ROS_DISTRO_RELEASE_DATE', True) == '' else \
                                        d.getVar('ROS_DISTRO_RELEASE_DATE', True) \
                                      }"

# Value determined by superflore and set in conf/ros-distro/include/ROS_DISTRO/generated/superflore-ros-distro.inc
ROS_NUM_CACHE_YAML_COMMITS ??= "0"
ROS_DISTRO_METADATA_VERSION_UPDATE = "${ROS_NUM_CACHE_YAML_COMMITS}"

require conf/ros-distro/milestone.conf
ROS_DISTRO_METADATA_VERSION_REVISION = "${ROS_DISTRO_METADATA_MILESTONE_NUMBER}"

ROS_DISTRO_METADATA_VERSION = "${ROS_DISTRO_METADATA_VERSION_MAJOR}.${ROS_DISTRO_METADATA_VERSION_MINOR}.${ROS_DISTRO_METADATA_VERSION_MICRO}.${ROS_DISTRO_METADATA_VERSION_UPDATE}"
ROS_DISTRO_METADATA_VERSION .= "-${ROS_DISTRO_METADATA_VERSION_REVISION}"

# If ROS_DISTRO is unset, the build will abort with a ParseError when bitbake attempts to include a file with ROS_DISTRO in its
# path below.
ROS_DISTRO ?= "${@bb.error('ROS_DISTRO must be set in conf/bblayers.conf')}"

# ROS_DISTRO_BASELINE_PLATFORM is set in ros-distro.inc .
require conf/ros-distro/include/${ROS_DISTRO_BASELINE_PLATFORM}/ros-distro-platform-preferred-providers.inc
require conf/ros-distro/include/${ROS_DISTRO_BASELINE_PLATFORM}/ros-distro-platform-recipe-blacklist.inc
require conf/ros-distro/include/${ROS_DISTRO_BASELINE_PLATFORM}/ros-distro-platform-preferred-versions.inc

# Default to showing DISTRO_NAME and the ROS build environment variable settings in the build configuration.
ROS_EXTRA_BUILDCFG_VARS ?= "DISTRO_NAME ROS_DISTRO ROS_VERSION ROS_PYTHON_VERSION"
BUILDCFG_VARS_append = " ${ROS_EXTRA_BUILDCFG_VARS}"

# Ignore metadata that requires meta-raspberrypi if it's not present.
BBMASK +=  "${@bb.utils.contains('BBFILE_COLLECTIONS', 'raspberrypi', '', 'recipes-bsp/bootfiles/rpi-config_%.bbappend', d)}"

# "lapack" needs FORTRAN support
require conf/ros-distro/include/enable-fortran.inc

require conf/ros-distro/include/ros-world-recipe-blacklist.inc
